; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=@APP_NAME@ @APP_VERSION@
AppVersion=@APP_VERSION@
AppVerName=@APP_NAME@ version @APP_VERSION@
AppPublisher=The UniDic Consortium.
AppPublisherURL=http://comainu.org/
AppSupportURL=http://comainu.org/
AppUpdatesURL=http://comainu.org/
AppCopyright=Copyright (C) 2010-2014 The UniDic Consortium.
; WindowVisible=no
; WizardImageFile=img\comainu.bmp
; WizardSmallImageFile=img\comainu.bmp
DefaultDirName={pf}\@APP_NAME@\@APP_VERSION@
UsePreviousAppDir=no
DefaultGroupName=@APP_NAME@ @APP_VERSION@
UsePreviousGroup=no
SourceDir=dist\@APP_NAME@-@APP_VERSION@
LicenseFile=License.txt
InfoBeforeFile=Environment.txt
; InfoAfterFile=Readme.txt
OutputDir=..
OutputBaseFilename=setup
Compression=bzip/9
ChangesAssociations=yes
UninstallDisplayIcon={app}\bin\runcom.exe
; MessagesFile=compiler:Japanese-3.0.5.isl
; MessagesFile=compiler:Japanese-2-3.0.5.isl
ShowLanguageDialog=no

[Languages]
Name: "en"; MessagesFile: "compiler:Default.isl"
Name: "ja"; MessagesFile: "compiler:Languages\Japanese.isl";

[Tasks]
Name: common; Description: "全ユーザ共通 (&A)"; GroupDescription: "インストールユーザ:"; Flags: exclusive unchecked
Name: user;  Description: "現在のユーザ (&U)"; GroupDescription: "インストールユーザ:"; Flags: exclusive
Name: "desktopicon"; Description: "デスクトップアイコンを作成する(&D)"; GroupDescription: "追加タスク："
; Name: "changeassoc"; Description: "データファイル(*.csp)を関連付ける(&F)"; GroupDescription: "追加タスク："

[Dirs]
Name: "{code:DefAppDataFolder}\@APP_NAME@"
Name: "{code:DefAppDataFolder}\@APP_NAME@\sample"
Name: "{code:DefAppDataFolder}\@APP_NAME@\tmp"
Name: "{code:DefAppDataFolder}\@APP_NAME@\out"

[InstallDelete]
Type: files; Name: "{code:DefAppDataFolder}\@APP_NAME@\wincomainu.conf";

[Files]
Source: "sample/*"; DestDir: "{code:DefAppDataFolder}\@APP_NAME@\sample"; Flags: recursesubdirs;
Source: "*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs;
Source: "README_GUI.txt"; DestDir: "{app}"; Flags: isreadme;
Source: "License.txt"; DestDir: "{app}";
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
function DefAppDataFolder(Param: String): String;
begin
  if IsTaskSelected('common') then
    Result := ExpandConstant('{commonappdata}')
  else
    Result := ExpandConstant('{userappdata}')
end;
function DefDesktop(Param: String): String;
begin
  if IsTaskSelected('common') then
    Result := ExpandConstant('{commondesktop}')
  else
    Result := ExpandConstant('{userdesktop}')
end;

[Icons]
Name: "{group}\1. {groupname}"; Filename: "{app}\bin\runcom.exe"; Parameters: """{app}\script\wincomainu.pl"""; WorkingDir: "{app}"
; Name: "{group}\2. {groupname} Help"; Filename: "{app}\@APP_NAME@.chm"
Name: "{group}\2. {groupname} Manual"; Filename: "{app}\doc\manual.pdf"
Name: "{group}\3. {groupname} License"; Filename: "{app}\License.txt"
Name: "{group}\4. {groupname} Readme"; Filename: "{app}\README_GUI.txt"
Name: "{group}\5. {groupname} Environment"; Filename: "{app}\Environment.txt"
Name: "{group}\6. {groupname} Auto setup path"; Filename: "{app}\bin\runcom.exe"; Parameters: """{app}\script\comainu_setup.pl"""; WorkingDir: "{app}"; Flags: dontcloseonexit; Comment: "右クリック：「管理者として実行」して下さい。"
Name: "{group}\7. Uninstall {groupname}"; Filename: "{uninstallexe}"
Name: "{code:DefDesktop}\@APP_NAME@-@APP_VERSION@"; Filename: "{app}\bin\runcom.exe"; Parameters: """{app}\script\wincomainu.pl"""; Tasks: desktopicon; WorkingDir: "{app}"

[Run]
Filename: "{app}\bin\runcom.exe"; Parameters: """{app}\script\comainu_setup.pl"""; StatusMsg: "外部ツールのパスを自動設定しています。この処理には数分かかることがあります。"; Flags: runhidden;
Filename: "{app}\bin\runcom.exe"; Parameters: """{app}\script\wincomainu.pl"""; Description: "Win@APP_NAME@ を起動する"; Flags: nowait postinstall skipifsilent; WorkingDir: "{app}"

[UninstallDelete]
Type: files; Name: "{app}\wincomainu_org.conf";
Type: filesandordirs; Name: "{app}\tmp";
Type: filesandordirs; Name: "{app}\script";
Type: filesandordirs; Name: "{code:DefAppDataFolder}\@APP_NAME@\tmp";

[Registry]
; Root: HKCR; Subkey: ".csp"; ValueType: string; ValueName: ""; ValueData: "@APP_NAME@.File"; Tasks: "changeassoc"; Flags: uninsdeletevalue 
; Root: HKCR; Subkey: ".csp"; ValueType: string; ValueName: "Content Type"; ValueData: "application/x-csp-file"; Tasks: "changeassoc"; Flags: uninsdeletevalue 
; Root: HKCR; Subkey: "@APP_NAME@.File"; ValueType: string; ValueName: ""; ValueData: "@APP_NAME@ File";  Tasks: "changeassoc"; Flags: uninsdeletekey 
; Root: HKCR; Subkey: "@APP_NAME@.File\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\bin\runcom.exe,0"; Tasks: "changeassoc"
; Root: HKCR; Subkey: "@APP_NAME@.File\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\runcom.exe"" ""%1"""; Tasks: "changeassoc"
